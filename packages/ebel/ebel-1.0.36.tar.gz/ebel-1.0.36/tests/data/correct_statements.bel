##################################################################################
# Document Properties
##################################################################################
SET DOCUMENT Authors = "Bruce Schultz, Christian Ebeling"
SET DOCUMENT Description = "Originally downloaded from https://github.com/OpenBEL/language/blob/master/version_2.0/bel2.0-example-statements.bel, used to test JSON generation."
SET DOCUMENT Name = "corrected_statements.bel"

###################################################################################
# Definitions Section

# NAMESPACES
DEFINE NAMESPACE MESHCS AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-cellular-structures/mesh-cellular-structures-20170725.belns"

DEFINE NAMESPACE CHEBI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/chebi/chebi-20180106.belns"
DEFINE NAMESPACE GOBP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-biological-process/go-biological-process-20190128.belns"
DEFINE NAMESPACE HGNC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc-human-genes/hgnc-human-genes-20170511.belns"
DEFINE NAMESPACE GOCC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-cellular-component/go-cellular-component-20170725.belns"
DEFINE NAMESPACE MESHD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-diseases/mesh-diseases-20170725.belns"
DEFINE NAMESPACE MESHPP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-processes/mesh-processes-20170511.belns"
#DEFINE NAMESPACE MESHA AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-anatomy/mesh-anatomy-20171030.belns"
DEFINE NAMESPACE MGI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20170725.belns"
DEFINE NAMESPACE RGD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/rgd-rat-genes/rgd-rat-genes-20170511.belns"
DEFINE NAMESPACE SCOMP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-named-complexes/selventa-named-complexes-20170511.belns"
DEFINE NAMESPACE SFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170511.belns"

DEFINE NAMESPACE dbSNP AS PATTERN "rs[0-9]+"

###################################################################################
# Statements Section
###################################################################################
a(CHEBI:"3',5'-cyclic AMP") increases act(p(HGNC:RAP1A))
a(CHEBI:"3',5'-cyclic GMP") increases surf(complex(p(HGNC:ITGAV),p(HGNC:ITGB5)))
a(CHEBI:"arachidonic acid") increases act(p(MGI:Mpo), ma(cat))
a(CHEBI:"fatty acid") increases act(p(HGNC:UCP1))
a(CHEBI:"nitric oxide") decreases r(HGNC:CFTR, var("c.1521_1523delCTT"))
a(CHEBI:"nitric oxide") increases surf(complex(p(HGNC:ITGA2),p(HGNC:ITGB1)))
a(CHEBI:"okadaic acid") increases p(HGNC:IRS1,pmod(Ph,Ser))
a(CHEBI:"oxidised LDL") increases act(p(HGNC:MMP2), ma(cat))
a(CHEBI:"oxidised LDL") increases surf(p(HGNC:MMP14))
a(CHEBI:"prostaglandin E2") increases act(p(HGNC:RAP1A))
a(CHEBI:"prostaglandin E2") decreases p(HGNC:PRKCD,pmod(Ph))
a(CHEBI:acetylcholine) increases act(p(HGNC:NOS3), ma(cat))
act(complex(p(HGNC:CCND1),p(HGNC:CDK4)), ma(kin)) directlyIncreases p(HGNC:RB1,pmod(Ph))
act(complex(SCOMP:"IkappaB Kinase Complex"), ma(kin)) increases act(complex(SCOMP:"Nfkb Complex"), ma(tscript))
act(complex(SCOMP:"IkappaB Kinase Complex"), ma(kin)) directlyIncreases deg(p(HGNC:NFKBIA))
act(complex(SCOMP:"p85/p110 PI3Kinase Complex"), ma(kin)) increases surf(p(HGNC:ATP1A2,pmod(Ph,Thr)))
act(complex(SCOMP:"TORC1 Complex"), ma(kin)) directlyIncreases p(MGI:Rps6kb1,pmod(Ph,Thr,412))
act(complex(SCOMP:"TORC2 Complex"), ma(kin)) directlyIncreases p(MGI:Eif4ebp1,pmod(Ph,Thr,36))
act(p(fus(HGNC:BCR, "?", HGNC:ABL1, "?"))) increases act(complex(SCOMP:"p85/p110 PI3Kinase Complex"), ma(kin))
act(p(fus(HGNC:BCR, "?", HGNC:ABL1, "?"))) increases p(HGNC:FOXO3,pmod(Ph,Ser,253))
act(p(fus(HGNC:EWSR1, "?", HGNC:FLI1, "?"))) directlyDecreases r(HGNC:CDKN1A)
act(p(fus(HGNC:PAX3, "?", HGNC:FOXO1, "?")), ma(tscript)) decreases r(HGNC:IGFBP5)
act(p(HGNC:"NKX2-5"), ma(tscript)) directlyIncreases r(HGNC:MIR143)
act(p(HGNC:AKT1), ma(kin)) increases act(p(HGNC:MDM2), ma(cat))
act(p(HGNC:AKT1), ma(kin)) directlyDecreases act(p(HGNC:PAWR))
act(p(HGNC:AKT1), ma(kin)) decreases complex(p(HGNC:AKT1),p(HGNC:CREBBP))
act(p(HGNC:AKT1), ma(kin)) increases complex(p(HGNC:SKP2),p(SFAM:"FOXO Family"))
act(p(HGNC:AKT1), ma(kin)) increases tloc(p(HGNC:CDKN1A), fromLoc(MESHCS:"Cell Nucleus"), toLoc(MESHCS:Cytoplasm))
act(p(HGNC:AKT1), ma(kin)) decreases tloc(p(SFAM:"FOXO Family"), fromLoc(MESHCS:Cytoplasm), toLoc(MESHCS:"Cell Nucleus"))
act(p(HGNC:CDK2), ma(kin)) directlyIncreases p(HGNC:CDKN1B,pmod(Ph,Thr,187))
act(p(HGNC:CTNNB1), ma(tscript)) directlyIncreases r(HGNC:CCND1)
act(p(HGNC:HMGA1)) increases surf(p(HGNC:INSR))
act(p(HGNC:HRAS), ma(gtp)) increases surf(p(HGNC:CDH5))
act(p(HGNC:LEF1), ma(tscript)) directlyIncreases r(HGNC:CCND1)
act(p(HGNC:MAP2K1), ma(kin)) increases surf(p(HGNC:ITGB3))
act(p(HGNC:NAA10), ma(cat)) directlyIncreases p(HGNC:HIF1A,pmod(Ac,K,532))
act(p(HGNC:PIM1)) increases surf(p(HGNC:KDR))
act(p(HGNC:PTEN), ma(phos)) decreases act(p(HGNC:AKT1), ma(kin))
act(p(HGNC:PTPRJ), ma(phos)) directlyDecreases act(p(HGNC:KDR), ma(kin))
act(p(HGNC:SGK1), ma(kin)) directlyIncreases p(HGNC:RAF1,pmod(Ph,S,259))
act(p(HGNC:SOD2), ma(cat)) increases rxn(reactants(a(CHEBI:superoxide)),products(a(CHEBI:"hydrogen peroxide")))
act(p(HGNC:SRC), ma(kin)) directlyIncreases p(HGNC:CDH5,pmod(Ph,Y))
act(p(HGNC:SRF), ma(tscript)) directlyIncreases r(HGNC:MIR143)
act(p(HGNC:TIMP2), ma(pep)) decreases p(HGNC:MMP9)
act(p(HGNC:TNFRSF1A)) regulates act(complex(SCOMP:"AP-1 Complex"), ma(tscript))
act(p(HGNC:USF1), ma(tscript)) causesNoChange r(HGNC:IGF2R)
act(p(HGNC:VHL), ma(cat)) directlyIncreases deg(p(HGNC:PRKCI))
act(p(MGI:Akt1), ma(kin)) directlyIncreases p(MGI:Foxo3,pmod(Ph,Thr,32))
act(p(MGI:Akt1), ma(kin)) directlyIncreases p(MGI:Tsc2,pmod(Ph,Ser,939))
act(p(MGI:Akt1), ma(kin)) decreases r(MGI:Cdkn1b)
act(p(MGI:Cebpd), ma(tscript)) association bp(MESHPP:Apoptosis)
act(p(MGI:Chuk), ma(kin)) increases deg(p(MGI:Nfkbia))
act(p(MGI:Epas1), ma(tscript)) increases surf(complex(p(MGI:Itgav),p(MGI:Itgb3)))
act(p(MGI:Foxp3), ma(tscript)) decreases sec(p(MGI:Ifng))
act(p(MGI:Ikbkb), ma(kin)) increases deg(p(MGI:Nfkbia))
act(p(MGI:Inppl1), ma(phos)) negativeCorrelation path(MESHD:Hyperglycemia)
act(p(MGI:Itch), ma(cat)) directlyIncreases deg(p(MGI:Trp63))
act(p(MGI:Mtor), ma(kin)) directlyIncreases p(MGI:Eif4ebp1,pmod(Ph,Thr,36))
act(p(MGI:Ppargc1a), ma(tscript)) directlyIncreases act(p(SFAM:"MEF2 Family"), ma(tscript))
act(p(MGI:Tcf3), ma(tscript)) directlyIncreases r(MGI:Bmp4)
act(p(MGI:Tlr2), ma(cat)) increases sec(p(MGI:Ccl2))
act(p(SFAM:"AKT Family"), ma(kin)) directlyIncreases p(HGNC:GSK3B,pmod(Ph,Ser,9))
act(p(SFAM:"RAS Family"), ma(gtp)) increases act(p(SFAM:"RAC Family"), ma(gtp))
act(p(SFAM:"RAS Family"), ma(gtp)) increases complex(p(HGNC:RAF1),p(HGNC:BRAF))
act(p(SFAM:"RAS Family"), ma(gtp)) increases p(HGNC:RAF1,pmod(Ph,S,338))
act(p(SFAM:"RAS Family"), ma(gtp)) positiveCorrelation path(MESHD:"Carcinoma, Non-Small-Cell Lung")
act(p(SFAM:"RAS Family"), ma(gtp)) directlyIncreases tloc(p(HGNC:BRAF), fromLoc(MESHCS:"Intracellular Space"), toLoc(MESHCS:"Cell Membrane"))
bp(MESHPP:Obesity) negativeCorrelation act(p(HGNC:SLC2A4), ma(tport))
bp(MESHPP:Obesity) positiveCorrelation act(p(MGI:Trp53), ma(tscript))
complex(p(HGNC:CTNNB1),p(HGNC:LEF1)) directlyIncreases act(p(HGNC:CTNNB1), ma(tscript))
complex(p(HGNC:F3),p(HGNC:F7)) increases act(p(HGNC:F10), ma(pep))
complex(p(HGNC:HIF1A),p(HGNC:EP300)) directlyIncreases act(p(HGNC:HIF1A), ma(tscript))
complex(p(HGNC:ITGA6),p(HGNC:ITGB1)) increases act(p(HGNC:SYK), ma(kin))
complex(p(HGNC:ITGB1),p(HGNC:FN1)) increases act(p(HGNC:PRKCA), ma(kin))
complex(p(HGNC:MAP3K11),p(HGNC:SH3RF1)) increases p(HGNC:JUN,pmod(Ph))
complex(p(HGNC:RBL2),p(SFAM:"E2F Family")) directlyDecreases complex(p(SFAM:"E2F Family"),p(HGNC:EP300))
composite(p(HGNC:RAF1),p(HGNC:AKT1)) regulates act(p(HGNC:CDK2), ma(kin))
composite(p(HGNC:VEGFA),p(HGNC:FN1)) increases act(p(HGNC:PRKCA), ma(kin))
composite(p(HGNC:VEGFA),p(HGNC:FN1)) increases surf(p(HGNC:PRKCA))
p(HGNC:AKT1, loc(MESHCS:Cytoplasm)) increases p(HGNC:EGFR, loc(MESHCS:Cytoplasm))
p(HGNC:BCL2) causesNoChange act(p(HGNC:PAWR))
p(HGNC:BCL2, loc(MESHCS:Mitochondria)) increases p(HGNC:CYCS, loc(MESHCS:Mitochondria))
p(HGNC:BRAF,var("p.Val599Glu")) directlyIncreases act(p(HGNC:BRAF), ma(kin))
p(HGNC:CALM1) regulates act(p(HGNC:NOS3), ma(cat))
p(HGNC:CASP3) increases p(HGNC:APC, frag("1_?"))
p(HGNC:CASP3) increases p(HGNC:HTT, frag("?"))
p(HGNC:CASP8) increases p(HGNC:CFLAR, frag("?","36kD"))
p(HGNC:CAV1) directlyDecreases act(p(HGNC:KDR), ma(kin))
p(HGNC:CAV1,pmod(Ph,Y,14)) directlyDecreases complex(p(HGNC:CAV1),p(HGNC:KDR))
p(HGNC:CDKN1A,pmod(Ph,Ser,190)) decreases deg(p(HGNC:CDKN1A))
p(HGNC:CDKN1B,pmod(Ph,Thr,187)) directlyIncreases complex(p(HGNC:SKP2),p(HGNC:CDKN1B))
p(HGNC:CDKN1B,pmod(Ub)) directlyIncreases deg(p(HGNC:CDKN1B))
p(HGNC:CFTR, var("?")) regulates p(HGNC:NFKBIA)
p(HGNC:CFTR, var("=")) increases act(p(HGNC:SLC9A3))
p(HGNC:CTNNB1,var("p.Thr41Ala")) directlyIncreases act(p(HGNC:CTNNB1), ma(tscript))
p(HGNC:EDN3) causesNoChange act(p(HGNC:EDNRA))
p(HGNC:EGF) increases p(HGNC:EGFR, loc(GOCC:"cell surface"))
p(HGNC:EGF) increases p(HGNC:JAK2,pmod(Ph,Tyr))
p(HGNC:EGF) increases tloc(p(SFAM:"STAT5 Family"), fromLoc(MESHCS:Cytoplasm), toLoc(MESHCS:"Cell Nucleus"))
p(HGNC:EGFR,pmod(Ub,K)) directlyIncreases deg(p(HGNC:EGFR))
p(HGNC:ELANE) -> p(HGNC:MUC5AC,pmod(Glyco))
p(HGNC:ELK1,var("p.Trp379Ala")) directlyDecreases act(p(HGNC:ELK1), ma(tscript))
p(HGNC:EPO) increases p(HGNC:RAF1,pmod(Ph,Tyr,341))
p(HGNC:ERBB3) causesNoChange p(SFAM:"AKT Family",pmod(Ph,Thr,493))
p(HGNC:EZH2) positiveCorrelation complex(p(SFAM:"Histone H3 Family",pmod(Me,Lys,27)),g(HGNC:DKK1))
p(HGNC:EZH2) positiveCorrelation complex(p(SFAM:"Histone H3 Family",pmod(Me,Lys,27)),g(HGNC:DKK1))
p(HGNC:F2) increases sec(a(CHEBI:"nitric oxide"))
p(HGNC:F2) increases sec(p(HGNC:VWF))
p(HGNC:F2) increases surf(p(HGNC:F3))
p(HGNC:F2) increases tloc(p(HGNC:SELP), fromLoc(MESHCS:"Cytoplasmic Granules"), toLoc(MESHCS:"Cell Membrane"))
p(HGNC:GP6) regulates act(p(HGNC:SYK), ma(kin))
p(HGNC:GSK3A,pmod(Ph,Ser,21)) directlyDecreases act(p(HGNC:GSK3A), ma(kin))
p(HGNC:HDAC4,pmod(Sumo,Lys,559)) directlyIncreases act(p(HGNC:HDAC4), ma(cat))
p(HGNC:HIF1A,pmod(Ac,K,532)) directlyIncreases deg(p(HGNC:HIF1A))
p(HGNC:HIF1A,pmod(Hy,Asn,863)) directlyDecreases act(p(HGNC:HIF1A), ma(tscript))
p(HGNC:HIF1A,pmod(Hy,Pro,402)) directlyIncreases complex(p(HGNC:VHL),p(HGNC:HIF1A))
p(HGNC:HIF1A,pmod(Hy,Pro,564)) directlyIncreases complex(p(HGNC:VHL),p(HGNC:HIF1A))
p(HGNC:HRAS,var("p.Gly12Val")) decreases sec(p(HGNC:CASP3))
p(HGNC:IGF1) directlyIncreases act(p(HGNC:IGF1R), ma(kin))
p(HGNC:IGF1) regulates p(SFAM:"AKT Family",pmod(Ph,Ser,473))
p(HGNC:IKBKB,pmod(Ph)) increases act(complex(SCOMP:"Nfkb Complex"), ma(tscript))
p(HGNC:IL13) increases r(HGNC:CFTR, var("r.1653_1655delcuu"))
p(HGNC:IL1B) increases deg(p(HGNC:NFKBIA))
p(HGNC:IRS1,pmod(Ph,Ser)) negativeCorrelation p(HGNC:IRS1,pmod(Ph,Tyr))
p(HGNC:IRS1,pmod(Glyco,Ser)) directlyDecreases act(p(HGNC:IRS1), ma(cat))
p(HGNC:IRS2,pmod(Ph,Ser)) negativeCorrelation p(HGNC:IRS2,pmod(Ph,Tyr))
p(HGNC:LRIG1) increases p(HGNC:EGFR,pmod(Ub,K))
p(HGNC:MYD88) causesNoChange r(HGNC:IFNB1)
p(HGNC:NRAS,var("p.Gln61Arg")) increases p(HGNC:RPS6KB1,pmod(Ph,Ser,447))
p(HGNC:PAWR,pmod(Ph,Thr,163)) directlyIncreases act(p(HGNC:PAWR))
p(HGNC:PRKACA) increases p(HGNC:GSK3A,pmod(Ph,Ser,21))
p(HGNC:PRKACA) increases p(HGNC:GSK3B,pmod(Ph,Ser,9))
p(HGNC:PRPF4B,pmod(Ph)) directlyDecreases complex(p(HGNC:PRPF4B),p(HGNC:PRPF6))
p(HGNC:PRPF4B,pmod(Ph)) directlyDecreases complex(p(HGNC:PRPF4B),p(HGNC:SMARCA4))
p(HGNC:PTEN) negativeCorrelation p(SFAM:"AKT Family",pmod(Ph))
p(HGNC:PTEN) negativeCorrelation p(SFAM:"AKT Family",pmod(Ph))
p(HGNC:PTEN,var("p.Arg173Cys")) decreases p(HGNC:PTEN)
p(HGNC:RAF1,pmod(Ph,Ser,621)) directlyIncreases complex(p(HGNC:RAF1),p(SFAM:"14-3-3 Family"))
p(HGNC:RELA,pmod(Ac,Lys,310)) increases act(complex(SCOMP:"Nfkb Complex"), ma(tscript))
p(HGNC:SELE) increases p(HGNC:HSPB1,pmod(Ph))
p(HGNC:SIRT1) negativeCorrelation complex(p(SFAM:"Histone H4 Family",pmod(Ac,Lys,16)),g(HGNC:DKK1))
p(HGNC:SNRPC) causesNoChange act(p(fus(HGNC:EWSR1, "?", HGNC:FLI1, "?")))
p(HGNC:SUZ12) positiveCorrelation complex(p(SFAM:"Histone H3 Family",pmod(Me,Lys,27)),g(HGNC:DKK1))
p(HGNC:TGFB1) increases p(HGNC:CFTR, var("p.Thr1220Lysfs"))
p(HGNC:TGFB1) association p(HGNC:COL4A1, loc(GOCC:"extracellular matrix"))
p(HGNC:TIRAP) causesNoChange r(HGNC:IFNB1)
p(HGNC:TNF) increases act(complex(SCOMP:"Nfkb Complex"), ma(tscript))
p(HGNC:TNF) directlyIncreases act(p(HGNC:TNFRSF1A))
p(HGNC:TNF) increases surf(p(HGNC:MMP14))
p(HGNC:TP53,var("p.Arg175Leu")) decreases p(HGNC:TP53)
p(HGNC:TP53,var("p.Arg181His")) directlyDecreases act(p(HGNC:TP53), ma(tscript))
p(HGNC:TP53,var("p.Arg248Gln")) directlyDecreases act(p(HGNC:TP53), ma(tscript))
p(HGNC:TP53,var("p.Arg282Trp")) directlyDecreases act(p(HGNC:TP53), ma(tscript))
p(HGNC:TP53,var("p.Asn239Ser")) directlyDecreases act(p(HGNC:TP53), ma(tscript))
p(HGNC:TP53,var("p.Asp281Glu")) directlyDecreases act(p(HGNC:TP53), ma(tscript))
p(HGNC:TP53,var("p.Pro309Ser")) directlyDecreases act(p(HGNC:TP53), ma(tscript))
p(HGNC:TP63) increases composite(p(SFAM:"Histone H3 Family",pmod(Ac)),p(SFAM:"Histone H4 Family",pmod(Ac)))
p(HGNC:TP73,var("p.Arg263Trp")) directlyDecreases act(p(HGNC:TP73), ma(tscript))
p(HGNC:UCP1,var("p.Met229Leu")) positiveCorrelation path(MESHD:"Diabetes Mellitus, Type 2")
p(HGNC:UCP1,var("p.Met229Leu")) positiveCorrelation path(MESHD:"Diabetes Mellitus, Type 2")
p(HGNC:VEGFA) increases p(HGNC:FLT1,pmod(Ph,Y))
p(HGNC:VEGFA) increases p(HGNC:PTK2B,pmod(Ph))
p(HGNC:VEGFA) regulates p(SFAM:"PIK3R Class IA Family",pmod(Ph,Tyr))
p(HGNC:VHL) decreases act(p(HGNC:HIF1A), ma(tscript))
p(HGNC:VHL) directlyIncreases deg(p(HGNC:HIF1A))
p(HGNC:VHL) decreases surf(p(HGNC:CXCR4))
p(HGNC:VHL,var("p.Ile180Asn")) decreases p(HGNC:VHL)
p(HGNC:VTN) directlyIncreases act(complex(p(HGNC:ITGAV),p(HGNC:ITGB3)))
p(HGNC:VWF) directlyDecreases deg(p(HGNC:F8))
p(MGI:Akt1,pmod(Ph,Ser,473)) directlyIncreases p(MGI:Foxo3,pmod(Ph,Thr,32))
p(MGI:Angpt1) directlyIncreases act(p(MGI:Tek), ma(kin))
p(MGI:Braf,var("p.Val600Glu")) increases p(MGI:Sftpc)
p(MGI:Ctnnb1) increases act(p(MGI:Mapk1), ma(kin))
p(MGI:Egf) regulates p(MGI:Akt1)
p(MGI:Hck,var("p.Tyr499Phe")) directlyIncreases act(p(MGI:Hck), ma(kin))
p(MGI:Hck,var("p.Tyr499Phe")) increases p(MGI:Il5)
p(MGI:Hck,var("p.Tyr499Phe")) increases path(MESHD:Inflammation)
p(MGI:Il6st,var("p.Tyr757Phe")) directlyDecreases act(p(MGI:Il6st), ma(cat))
p(MGI:Il6st,var("p.Tyr759Phe")) decreases bp(MESHPP:Apoptosis)
p(MGI:Irs1) causesNoChange p(MGI:Rps6kb2,pmod(Ph))
p(MGI:Jak3,var("p.Ala572Val")) increases p(MGI:Bcl2)
p(MGI:Kras,var("p.Gly12Asp")) increases path(MESHD:Neoplasms)
p(MGI:Mapkap1) causesNoChange p(MGI:Akt1,pmod(Ph,Ser,308))
p(MGI:Mapkap1) increases p(MGI:Akt1,pmod(Ph,Ser,473))
p(MGI:Mapkap1) increases p(MGI:Foxo3,pmod(Ph,Thr,32))
p(MGI:Pgf) directlyIncreases act(p(MGI:Flt1), ma(kin))
p(MGI:Rela,pmod(Ac,Lys,310)) directlyIncreases act(p(MGI:Rela), ma(tscript))
p(RGD:Chuk,pmod(Ph,Ser,180)) directlyIncreases act(p(RGD:Chuk), ma(kin))
p(RGD:Igf1) increases act(p(SFAM:"RAS Family"), ma(gtp))
p(RGD:Igf1) increases p(SFAM:"RPS6KB Family",pmod(Ph,Thr,412))
p(RGD:Igf1) causesNoChange r(RGD:Myog)
p(RGD:Ikbkb,pmod(Ph,Ser,177)) directlyIncreases act(p(RGD:Ikbkb), ma(kin))
p(RGD:Itgb4,pmod(Ph,Tyr)) directlyIncreases complex(p(RGD:Itga6),p(RGD:Itgb4),p(RGD:Shc1))
p(SFAM:"AKT Family") increases p(HGNC:IRAK1,pmod(Ph,Thr,100))
p(SFAM:"AKT Family",pmod(Ph,Ser,473)) positiveCorrelation p(SFAM:"AKT Family",pmod(Ph,Tyr,308))
p(SFAM:"AKT Family",pmod(Ph,Ser,473)) positiveCorrelation path(MESHD:"Carcinoma, Non-Small-Cell Lung")
p(SFAM:"FOXO Family",pmod(Ph)) increases tloc(p(SFAM:"FOXO Family"), fromLoc(MESHCS:Cytoplasm), toLoc(MESHCS:"Cell Nucleus"))
p(SFAM:"Histone H3 Family",pmod(Ac)) increases act(p(HGNC:SREBF1), ma(tscript))
p(SFAM:"Histone H4 Family",pmod(Me,Arg,3)) increases act(p(HGNC:YY1), ma(tscript))
p(SFAM:"IL1 Family") increases act(complex(SCOMP:"Nfkb Complex"), ma(tscript))
p(SFAM:"IL1 Family") increases act(p(HGNC:IRAK1), ma(kin))
p(SFAM:"IL1 Family") increases r(HGNC:SELE)
p(SFAM:"INS Family") increases p(MGI:Akt1,pmod(Ph,Ser,473))
p(SFAM:"IRS Family",pmod(Ph)) directlyIncreases complex(p(SFAM:"PIK3R Class IA Family"),p(SFAM:"IRS Family"))
p(SFAM:"PDGF Family") increases p(MGI:Akt1,pmod(Ph,Ser,473))
p(SFAM:"RAS Family",pmod(Farn)) directlyIncreases act(p(SFAM:"RAS Family"), ma(gtp))
p(SFAM:"RAS Family",pmod(Farn)) directlyIncreases tloc(p(SFAM:"RAS Family"), fromLoc(MESHCS:"Intracellular Space"), toLoc(MESHCS:"Cell Membrane"))
p(SFAM:"RAS Family",pmod(Farn,L)) directlyIncreases complex(p(SFAM:"RAS Family"),p(SFAM:"RAF Family"))
p(SFAM:"VEGF Family") increases act(p(HGNC:CYBB), ma(cat))
p(SFAM:"VEGF Family") directlyIncreases act(p(HGNC:KDR), ma(kin))
p(SFAM:"VEGF Family") increases complex(p(HGNC:ARF6),p(HGNC:RAC1))
p(SFAM:"VEGF Family") increases act(p(HGNC:RAC1), ma(gtp))
p(SFAM:"VEGF Family") increases p(HGNC:CAV1,pmod(Ph,Y,14))
p(SFAM:"VEGF Family") directlyIncreases p(HGNC:KDR,pmod(Ph,Y,1054))
rxn(reactants(a(CHEBI:glucose)),products(a(CHEBI:pyruvate))) subProcessOf bp(MESHPP:Glycolysis)
tloc(p(HGNC:CTNNB1), fromLoc(MESHCS:Cytoplasm), toLoc(MESHCS:"Cell Nucleus")) increases complex(p(HGNC:CTNNB1),p(HGNC:BCL9))
tloc(p(HGNC:HDAC4), fromLoc(MESHCS:Cytoplasm), toLoc(MESHCS:"Cell Nucleus")) directlyIncreases p(HGNC:HDAC4,pmod(Sumo,Lys,559))
tloc(p(HGNC:PAWR), fromLoc(MESHCS:Cytoplasm), toLoc(MESHCS:"Cell Nucleus")) decreases act(complex(SCOMP:"Nfkb Complex"), ma(tscript))
tloc(p(SFAM:"DVL Family"), fromLoc(MESHCS:Cytoplasm), toLoc(MESHCS:"Cell Membrane")) increases complex(p(SFAM:"DVL Family"),p(SFAM:"DVL Family"))
